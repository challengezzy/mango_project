<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" width="400" height="220" creationComplete="init()" title="上传JAR文件" >
	
	<fx:Metadata>		       
		[Event(name='loadDataSuccessful',type='smartx.flex.components.event.BasicEvent')]
	</fx:Metadata>
	
	<fx:Script>   
		<![CDATA[
//			import mx.controls.Alert;
			import mx.core.IFlexDisplayObject;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.remoting.RemoteObject;
			
			import smartx.flex.components.assets.AssetsFileLib;
			import smartx.flex.components.basic.ClientEnviorment;
			import smartx.flex.components.event.BasicEvent;
			import smartx.flex.components.util.LoadingWindow;
			import smartx.flex.components.util.SmartXMessage;
			import smartx.flex.components.vo.GlobalConst;
			
			private var destination:String = GlobalConst.SERVICE_FORM;
			private var formService:RemoteObject;
			private var bamService:RemoteObject;
			private var endpoint:String  = ClientEnviorment.getInstance().getVar(ClientEnviorment.KEY_SERVICE_ENDPOINT) as String;
			
			private var fileReference:FileReference = new FileReference();
			
			private var filterArray:Array = new Array();
			
			private var operatingWindow:IFlexDisplayObject;

			
			protected function ro_resultHandler(event:ResultEvent):void{
				endOperat();
				SmartXMessage.show("文件上传成功!");
				
			}
			
			protected function init():void{
				
				var jarfilter:FileFilter = new FileFilter("JAR文件", "*.JAR");
				var Allfilter:FileFilter = new FileFilter("所有文件", "*.*");
				
				filterArray.push(jarfilter);
				filterArray.push(Allfilter);
				
				formService = new RemoteObject(destination);
				bamService = new RemoteObject("smartXBAMService");
				
				if(endpoint != null){
					formService.endpoint = endpoint;
					bamService.endpoint = endpoint;
				}
				
				bamService.uploadJarFile.addEventListener(ResultEvent.RESULT,uploadJarFileHandler);
				bamService.uploadJarFile.addEventListener(FaultEvent.FAULT,faultHandler);
				
				//选择文件后，加载文件
				
				fileReference.addEventListener(Event.SELECT, selectHandler);
				
			}
			
			private function faultHandler(event:FaultEvent):void{
				endOperat();
				SmartXMessage.show(event.fault.faultString, SmartXMessage.MESSAGE_ERROR);
			}
			
			private function selectHandler(event:Event):void {
				
				fileReference.load();  //加载文件
				
				var tempFileName:String = fileReference.name;
				
				fileNameField.text = tempFileName;
				
				if(fileNameTxt.text == null || fileNameTxt.text == ""){
					fileNameTxt.text = tempFileName;
				}
				
				if(fileCodeTxt.text == null || fileCodeTxt.text == ""){
					fileCodeTxt.text = tempFileName;
				}
				
			}
			
			protected function btnBrows_clickHandler(event:MouseEvent):void{
				
				fileReference.browse(filterArray);
				
			}
			protected function btnUpload_clickHandler(event:MouseEvent):void{
				if(fileReference.data != null){
					var fileType:String = fileReference.type;
					if(fileType.toLocaleUpperCase()==".JAR"){
						
						var fileName:String  = fileNameTxt.text;
						var fileCode:String  = fileCodeTxt.text;
						var description:String = fileDescTxt.text;
						
						if(fileName == null || fileName == "" ){
							SmartXMessage.show("文件名不能为空!");
							return;
						}
						
						if(fileName.indexOf(".") == 0){
							SmartXMessage.show("文件名不合法!");
							return;
						}
						
						if(fileCode == null || fileCode == "" ){
							SmartXMessage.show("文件编码不能为空!");
							return;
						}
						
						var fileInfo:Object = new Object();
						
						fileInfo["filename"] = fileName;
						fileInfo["filecode"] = fileCode;
						fileInfo["description"] = description;
						
						startOperat();
						bamService.uploadJarFile(fileReference.data,fileInfo);
						
					}else{
						SmartXMessage.show("文件不合法,无法上传!必须是'*.JAR'类型的文件!");
					}
				}else{
					SmartXMessage.show("没有可上传的文件!");
				}

			}
			
			protected function uploadJarFileHandler(event:ResultEvent):void{
				endOperat();
				SmartXMessage.show("上传成功!");
				PopUpManager.removePopUp(this)
				dispatchEvent(new BasicEvent(BasicEvent.LOADDATA_SUCCESSFUL));
			}
			
			private function startOperat():void{
				
				if(operatingWindow == null){
					var temp:LoadingWindow = new LoadingWindow();
					temp.text = "正在上传……";
					operatingWindow = temp;
				}
				PopUpManager.addPopUp(operatingWindow,this,true);
				PopUpManager.centerPopUp(operatingWindow);
			}
			
			private function endOperat():void{
				if(operatingWindow!=null)
					PopUpManager.removePopUp(operatingWindow);
			}
		]]>   
	</fx:Script>       
	<mx:VBox width="100%" height="100%" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5" >
		<mx:HBox width="100%" horizontalAlign="left">
			<mx:Label text="文件:" />
			<mx:TextInput id="fileNameField" width="150" editable="false"/>
			<mx:Button id="btnBrows" click="btnBrows_clickHandler(event)" x="57" y="60" 
					   icon="{AssetsFileLib.browseIcon}" toolTip="选择文件" />
		</mx:HBox>
		<mx:HBox width="100%" horizontalAlign="left" >
			<mx:Label text="名称:" />
			<mx:TextInput id="fileNameTxt" width="100%" />
			<mx:Label text="编码:" />
			<mx:TextInput id="fileCodeTxt" width="100%" />
		</mx:HBox>
		<mx:VBox width="100%" >
			<mx:Label width="100%" text="描述:" />
			<mx:TextArea id="fileDescTxt" width="100%"/>
		</mx:VBox>
		<mx:HBox width="100%"  horizontalAlign="center">
			<mx:Button id="btnUpload" click="btnUpload_clickHandler(event)" label="确定" x="145" y="60" icon="{AssetsFileLib.uploadIcon}" />
			<mx:Button id="cancelButton" label="取消" click="PopUpManager.removePopUp(this)" icon="{AssetsFileLib.closeIcon}" />
		</mx:HBox>
	</mx:VBox>
</mx:TitleWindow>
