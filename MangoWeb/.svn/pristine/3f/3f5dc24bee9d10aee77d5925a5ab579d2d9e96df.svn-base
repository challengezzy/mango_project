<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx" 
		   fontSize="12" xmlns:ns1="smartx.flex.components.*" creationComplete="init()" 
		   xmlns:viewStackEffects="org.efflex.mx.viewStackEffects.*" 
		   xmlns:core="smartx.flex.components.core.*"
		    >
	<fx:Metadata>		       
		[Event(name='edit',type='smartx.flex.components.event.StyleTemplateEvent')]
		[Event(name='insert',type='smartx.flex.components.event.StyleTemplateEvent')]
		[Event(name='beforeSave',type='smartx.flex.components.event.StyleTemplateEvent')]
		[Event(name='save',type='smartx.flex.components.event.StyleTemplateEvent')]
		[Event(name='returnToList',type='smartx.flex.components.event.StyleTemplateEvent')]
		[Event(name='view',type='smartx.flex.components.event.StyleTemplateEvent')]
	</fx:Metadata>
	<fx:Declarations>
		<mx:HBox width="100%" id="listButtonBox" paddingLeft="5">
			<mx:PopUpMenuButton label="操作" id="popUpMenuButton" dataProvider="{menuArrayCol}" openAlways="true"/>
		</mx:HBox>
		<fx:Object id="viewButton" label="编辑" handler="{onEdit}" rightClick="true" />
		<fx:Object id="insertButton" label="新增"  handler="{onInsert}" rightClick="true" />
		<fx:Object id="deleteButton" label="删除"  handler="{deleteRow}" rightClick="true" />
		<fx:Object id="startButton" label="启动" handler="{onStart}" rightClick="true" />
		<fx:Object id="stopButton" label="停止" handler="{onStop}" rightClick="true" />
		<fx:Object id="saveButton" label="另存为" handler="{saveAs}" rightClick="true" />
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.Menu;
			import mx.core.IFlexDisplayObject;
			import mx.effects.Effect;
			import mx.events.CloseEvent;
			import mx.events.MenuEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.remoting.mxml.RemoteObject;
			
			import org.efflex.mx.viewStackEffects.CoverFlowPapervision3D;
			
			import smartx.flex.components.assets.AssetsFileLib;
			import smartx.flex.components.basic.ClientEnviorment;
			import smartx.flex.components.basic.DeskTopFrame;
			import smartx.flex.components.event.StyleTemplateEvent;
			import smartx.flex.components.util.ConfirmUtil;
			import smartx.flex.components.util.LoadingWindow;
			import smartx.flex.components.util.TempletDataUtil;
			import smartx.flex.components.vo.GlobalConst;
			import smartx.flex.components.vo.SimpleComboxItemVO;
			import smartx.flex.components.vo.TempletVO;

			[Bindable]
			public var templetCode:String;
			[Bindable]
			public var debugMode:Boolean = false;
			[Bindable]
			public var editable:Boolean = false;
			[Bindable]
			public var switchEffect:Effect;
			private var returnAfterSave:Boolean = false;
			[Bindable]
			public var showSystemButton:Boolean = true;
			[Bindable]
			public var destination:String = GlobalConst.SERVICE_FORM;
			[Bindable]
			public var endpoint:String;
			[Bindable]
			public var initQueryCondition:String;
			[Bindable]
			public var scenarioId:String;
			
			[Bindable]
			private var menuArrayCol:ArrayCollection = new ArrayCollection();
			
			private var templetVO:TempletVO; 
			
			private var ruleCode:String;
			
			private var formService:RemoteObject;
			
			private var bamService:RemoteObject;
			
			private var userName:String;
			
			private var operatingWindow:IFlexDisplayObject;
			
			private var desktop:DeskTopFrame = ClientEnviorment.getInstance().getVar(ClientEnviorment.DESKTOP_FRAME) as DeskTopFrame;
			
			private function init():void{
				if(templetCode == null)
					throw Error("元原模板编号不能为null");
				if(switchEffect == null)
					switchEffect = new CoverFlowPapervision3D();
				
				formService = new RemoteObject(destination);
				bamService = new RemoteObject("smartXBAMService");
				if(endpoint!=null){
					formService.endpoint = endpoint;
					bamService.endpoint = endpoint;
				}
					
				
				formService.getTempletVO.addEventListener(ResultEvent.RESULT,getTempletVOHandler);
				formService.getTempletVO.addEventListener(FaultEvent.FAULT,faultHandler);
				formService.getTempletVO(templetCode);
				
				bamService.startBusinessScenario.addEventListener(ResultEvent.RESULT,startBusinessScenarioHandler);
				bamService.startBusinessScenario.addEventListener(FaultEvent.FAULT,faultHandler);
				
				bamService.stopBusinessScenario.addEventListener(ResultEvent.RESULT,stopBusinessScenarioHandler);
				bamService.stopBusinessScenario.addEventListener(FaultEvent.FAULT,faultHandler);
				
				userName = String(ClientEnviorment.getInstance().getVar(ClientEnviorment.KEY_LOGIN_USER_LOGINNAME));
				
				cardPanel.insertAfInterceptor = "smartx.bam.bs.scenariomnager.RuleInsertAfFormInterceptor";
				cardPanel.updateBfInterceptor = "smartx.bam.bs.scenariomnager.RuleUpdateBfFormInterceptor";
				cardPanel.updateAfInterceptor = "smartx.bam.bs.scenariomnager.RuleUpdateAfFormInterceptor";
				
				listPanel.bfInterceptor = "smartx.bam.bs.scenariomnager.RuleDeleteBfFormInterceptor";
				
				listPanel.isShowAlert = false;
				
				listPanel.query(false,true,true);
				
				initPopMenuButton();
				
				if(listPanel.rightClickMenu != null){
					listPanel.rightClickMenu.addEventListener(MenuEvent.ITEM_CLICK,popUpMenuButtonClickHandler,false,0,true);
					for each(var obj:* in menuArrayCol){
						if(obj.rightClick){
							//需要添加到右键菜单
							listPanel.rightClickMenuItems.push(obj);
						}
					}
				}
				
			}
			
			private function initPopMenuButton():void{
				
				menuArrayCol.removeAll();
				menuArrayCol.addItem(viewButton);
				menuArrayCol.addItem(insertButton);
				menuArrayCol.addItem(deleteButton);
				menuArrayCol.addItem({type:"separator"});
				menuArrayCol.addItem(startButton);
				menuArrayCol.addItem(stopButton);
				menuArrayCol.addItem({type:"separator"});
				menuArrayCol.addItem(saveButton);
				
				var menuPop:Menu = popUpMenuButton.popUp as Menu;
				menuPop.addEventListener(MenuEvent.ITEM_CLICK,popUpMenuButtonClickHandler);
			}
			
			override protected function createChildren():void{
				super.createChildren();
				
				listPanel.topArea.addChild(listButtonBox);
				
			}
			
			private function startBusinessScenarioHandler(event:ResultEvent):void{
				endOperat();
				listPanel.query(false,true,true);
				Alert.show("规则已启动!","提示");
			}

			private function stopBusinessScenarioHandler(event:ResultEvent):void{
				endOperat();
				listPanel.query(false,true,true);
				Alert.show("规则已停止!","提示");
			}
			
			private function getTempletVOHandler(event:ResultEvent):void{
				templetVO = event.result as TempletVO;
			}
			
			private function faultHandler(event:FaultEvent):void{
				endOperat();
				listPanel.query(false,true,true);
				Alert.show(event.fault.faultString, 'Error');
			}
			
			public function onReturn():void{
				listPanel.refreshData();
				mainStack.selectedChild = listBox;
				dispatchEvent(new StyleTemplateEvent(StyleTemplateEvent.RETURN_TO_LIST));
			}
			
			public function isListPanelShowing():Boolean{
				return (mainStack.selectedChild == listBox);
			}
			
			public function onEdit():void{
				if(listPanel.getSelectedRowValue() != null){
					var row:Object = listPanel.getSelectedRowValue();
					cardPanel.editable = editable;
					cardPanel.setDataValue(row);
					mainStack.selectedChild = cardBox;
					
					TempletDataUtil.setTempletValue("OPERATOR",userName,row,templetVO,function():void{
						cardPanel.setDataValue(row);
					},null,this.endpoint);
					
					cardPanel.insertMode = false;
					
					if(editable)
						dispatchEvent(new StyleTemplateEvent(StyleTemplateEvent.EDIT));
					else
						dispatchEvent(new StyleTemplateEvent(StyleTemplateEvent.VIEW));
				}
				else
					Alert.show("请选择一条记录");
			}
			
			public function onInsert():void{
				//cardPanel.editable = true;
				
				var obj:Object = new Object();
				
				cardPanel.setDataValue(obj);
				mainStack.selectedChild = cardBox;
				
				TempletDataUtil.setTempletValue("BUSINESSSCENARIOID",this.scenarioId,obj,templetVO,function():void{
					cardPanel.setDataValue(obj);
				},null,this.endpoint);
				
				TempletDataUtil.setTempletValue("OPERATOR",userName,obj,templetVO,function():void{
					cardPanel.setDataValue(obj);
				},null,this.endpoint);
				
				cardPanel.insertMode = true;
				dispatchEvent(new StyleTemplateEvent(StyleTemplateEvent.INSERT));
			}
			
			public function onSave():void{
				dispatchEvent(new StyleTemplateEvent(StyleTemplateEvent.BEFORE_SAVE,cardPanel.getDataValue()));
				cardPanel.save();
				dispatchEvent(new StyleTemplateEvent(StyleTemplateEvent.SAVE));
			}
			
			public function onSaveAndReturn():void{
				onSave();
				returnAfterSave = true;
				listPanel.query(false,true,true);
			}
			private function onSaveSuccessful():void{
				if(returnAfterSave)
					onReturn();
				cardPanel.insertMode = false;
				returnAfterSave = false;
			}
			
			private function onStart():void{
				
				var item:* = listPanel.getSelectedRowValue();
				
				if(item == null){
					Alert.show("请先选择一条数据!","提示");
				}else{
					
					var comboxVO:SimpleComboxItemVO = item["STATUS"] as SimpleComboxItemVO;
					
					if(comboxVO.id == "1"){
						Alert.show("该规则已是启动状态无需多次启动!","提示");
					}else{
						startOperat("正在启动规则,请稍后.....");
						ruleCode = String(item["CODE"]);
						bamService.startBusinessScenario(ruleCode);
					}
				}
				
			}
			
			private function onStop():void{
				
				var item:* = listPanel.getSelectedRowValue();
				
				if(item == null){
					Alert.show("请先选择一条数据!","提示");
				}else{
					
					var comboxVO:SimpleComboxItemVO = item["STATUS"] as SimpleComboxItemVO;
					
					if(comboxVO.id == "0"){
						Alert.show("该规则已是停止状态无需多次停止!","提示");
					}else{
						startOperat("正在停止规则,请稍后.....");
						ruleCode = String(item["CODE"]);
						bamService.stopBusinessScenario(ruleCode);
					}

					
				}
			}
			
			private function deleteRow():void{
				
				var item:* = listPanel.getSelectedRowValue();
				
				if(item == null){
					Alert.show("请先选择一条数据!","提示");
				}else{
					listPanel.deleteRow();
				}
			}

			
			private function saveAs():void{
				
				var item:Object = listPanel.getSelectedRowValue();
				
				if(item != null){
					
					var arcp:AlertRuleCopyPanel = new AlertRuleCopyPanel();
					arcp.item = item;
					arcp.listPanel = listPanel;
					arcp.endpoint = endpoint;
					
					PopUpManager.addPopUp(arcp,desktop,true);
					PopUpManager.centerPopUp(arcp);
					
					
				}else{
					Alert.show("请先选择一条数据!","提示");
				}
				
			}

			protected function ruleBtn_clickHandler(event:MouseEvent):void
			{
				var conApp:AlertRuleAdvPanel = new AlertRuleAdvPanel();
				conApp.endpoint = endpoint;
				conApp.scenarioId = scenarioId;
				conApp.bcp = cardPanel;
				
				PopUpManager.addPopUp(conApp,desktop,true);
				PopUpManager.centerPopUp(conApp);
			}
			
			protected function popUpMenuButtonClickHandler(event:MenuEvent):void
			{
				var item:* = event.item;
				if(item.handler != null){
					item.handler();
				}
			}
			
			private function startOperat(text:String):void{
				
				if(operatingWindow == null){
					var temp:LoadingWindow = new LoadingWindow();
					temp.text = text;
					operatingWindow = temp;
				}
				PopUpManager.addPopUp(operatingWindow,desktop,true);
				PopUpManager.centerPopUp(operatingWindow);
			}
			
			private function endOperat():void{
				if(operatingWindow!=null)
					PopUpManager.removePopUp(operatingWindow);
			}
			
		]]>
	</fx:Script>
	<mx:ViewStack width="100%" height="100%" id="mainStack" creationPolicy="all" >
		<mx:VBox width="100%" height="100%" id="listBox" showEffect="{switchEffect}" hideEffect="{switchEffect}">
			<core:BillListPanel showLoadingWindow="false" showInitingWindow="false" destination="{destination}" endpoint="{endpoint}" 
								doubleClick="onEdit()" templetCode="{templetCode}" id="listPanel" editable="false" width="100%" height="100%" 
								showEditBox="false" debugMode="{debugMode}" initQueryCondition="{initQueryCondition}">
			</core:BillListPanel>
		</mx:VBox>
		<mx:VBox label="" width="100%" height="100%" id="cardBox" showEffect="{switchEffect}" hideEffect="{switchEffect}">
			<mx:HBox width="100%" id="cardButtonBox">
				<mx:Button id="returnButton" label="返回" toolTip="返回列表" click="onReturn()" 
						   icon="{AssetsFileLib.returnIcon}" />
				<mx:Button id="saveAndReturnButton" label="保存并返回" toolTip="保存当前信息并返回列表" 
						   click="onSaveAndReturn()" icon="{AssetsFileLib.savereturnIcon}" />
				<mx:Button id="cardSaveButton" label="保存" toolTip="保存当前信息" click="onSave()" icon="{AssetsFileLib.saveIcon}" />
				<mx:Button id="ruleBtn" label="规则高级设置" toolTip="进入规则高级编辑界面,用户可灵活编辑规则" 
						   click="ruleBtn_clickHandler(event)" icon="{AssetsFileLib.setIcon}" />
			</mx:HBox>
			<core:BillCardPanel destination="{destination}" endpoint="{endpoint}" templetCode="{templetCode}" id="cardPanel" 
								width="100%" height="100%" title="详细信息" debugMode="{debugMode}" saveSuccessful="onSaveSuccessful()" 
								showInitingWindow="false">
			</core:BillCardPanel>
		</mx:VBox>
	</mx:ViewStack>	
</mx:Canvas>
