<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				layout="absolute" width="490" height="300" showCloseButton="true"
				close="PopUpManager.removePopUp(this)" title="GeoServer配置" 
				creationComplete="init()">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.remoting.mxml.RemoteObject;
			
			import smartx.bam.flex.vo.BAMConst;
			import smartx.flex.components.basic.ClientEnviorment;
			import smartx.flex.components.basic.DeskTopFrame;
			import smartx.flex.components.util.OperatingTipUtil;
			import smartx.flex.components.util.SmartXMessage;
			
			private var geoService:RemoteObject;
			
			public var endpoint:String;
			[Bindable]
			public var geoServerXml:XML;
			
			public var contentXml:XML;
			[Bindable]
			private var wsDp:ArrayCollection = new ArrayCollection();
			[Bindable]
			private var dsDp:ArrayCollection = new ArrayCollection();
			
			private var initCompleted:Boolean = false;//是否初始化完毕
			
			private var dqGisLayermanager:DQGisLayerManager;
			
			private var desktop:DeskTopFrame = ClientEnviorment.getInstance().getVar(ClientEnviorment.DESKTOP_FRAME) as DeskTopFrame;
			
			private function init():void{
				geoService = new RemoteObject(BAMConst.BAM_Service);
				if(endpoint)
					geoService.endpoint = endpoint;
				
				geoService.getAllWorkSpaceName.addEventListener(ResultEvent.RESULT,getAllWorkSpaceNameHandler);
				geoService.getAllWorkSpaceName.addEventListener(FaultEvent.FAULT,function(event:FaultEvent):void{
					SmartXMessage.show("查询WorkSpace错误！",SmartXMessage.MESSAGE_ERROR);
					wsDp.removeAll();
					dsDp.removeAll();
					OperatingTipUtil.endOperat();
				});
				geoService.getDataStoreByWorkSpaceName.addEventListener(ResultEvent.RESULT,getDataStoreByWorkSpaceNameHandler);
				geoService.getDataStoreByWorkSpaceName.addEventListener(FaultEvent.FAULT,function(event:FaultEvent):void{
					SmartXMessage.show("查询DataStore错误！",SmartXMessage.MESSAGE_ERROR);
					dsDp.removeAll();
					OperatingTipUtil.endOperat();
				});
				initCompleted = true;
				initData();
			}
			
			private function getAllWorkSpaceNameHandler(event:ResultEvent):void{
				wsDp.removeAll();
				var result:ArrayCollection = event.result as ArrayCollection;
				var flag:Boolean = false;//是否有默认的WS
				for each(var data:String in result){
					wsDp.addItem(data);
					if(data == String(geoServerXml.workspace)){
						wsCbo.selectedItem = String(geoServerXml.workspace);
						flag = true
					}
				}
				
				if(flag)//如果有默认WorkSpace则继续查询对应的DataStore
					wsCbo_changeHandler(null);
				else{
					wsCbo.selectedIndex = -1;
					geoServerXml.workspace = "";
				}
				OperatingTipUtil.endOperat();
			}
			
			private function getDataStoreByWorkSpaceNameHandler(event:ResultEvent):void{
				dsDp.removeAll();
				var result:ArrayCollection = event.result as ArrayCollection;
				var flag:Boolean = false;//是否有默认的DS
				for each(var data:String in result){
					dsDp.addItem(data);
					if(data == String(geoServerXml.store)){
						dsCbo.selectedItem = String(geoServerXml.store);
						flag = true;
					}
				}
				if(!flag){//如果没有默认DS则设置为空
					dsCbo.selectedIndex = -1;
					geoServerXml.store = "";
				}
			}
			
			private function urlTxt_changeHandler(event:Event):void{
				geoServerXml.@url = urlTxt.text;
			}
			
			private function userTxt_changeHandler(event:Event):void{
				geoServerXml.@user = userTxt.text;
			}
			
			private function pwdTxt_changeHandler(event:Event):void{
				geoServerXml.@pwd = pwdTxt.text;	
			}
			
			private function wsCbo_changeHandler(event:ListEvent):void{
				geoServerXml.workspace = String(wsCbo.selectedItem==null?"":wsCbo.selectedItem);
				geoService.getDataStoreByWorkSpaceName(String(geoServerXml.@url),String(geoServerXml.@user)
													,String(geoServerXml.@pwd),String(wsCbo.selectedItem==null?"":wsCbo.selectedItem));
			}
			
			private function dsCbo_changeHandler(event:ListEvent):void{
				geoServerXml.store = String(dsCbo.selectedItem==null?"":dsCbo.selectedItem);
			}
			
			private function queryAllWsBtn_clickHandler(event:MouseEvent):void{
				OperatingTipUtil.startOperat("初始化中...",desktop);
				geoService.getAllWorkSpaceName(String(geoServerXml.@url),String(geoServerXml.@user),String(geoServerXml.@pwd));
			}
			
			private function initData():void{
				if(String(geoServerXml.@url) != '' 
					&& String(geoServerXml.@user) != ''
					&& String(geoServerXml.@pwd) != '')
					queryAllWsBtn_clickHandler(null);
			}
			
			public function refresh():void{
				if(initCompleted)
					initData();
			}
			
			public function layoutManagerBtn_clickHandler(event:MouseEvent):void{
				dqGisLayermanager = new DQGisLayerManager();
				dqGisLayermanager.contentXml = contentXml;
				dqGisLayermanager.endpoint = endpoint;
				
				PopUpManager.addPopUp(dqGisLayermanager,desktop,true);
				PopUpManager.centerPopUp(dqGisLayermanager);
				dqGisLayermanager.y = 100;
			}
			
		]]>
	</fx:Script>
	
	<fx:Script>
		<![CDATA[
			import com.hurlant.eval.ast.Void;
			import mx.events.FlexEvent;
			import mx.events.CloseEvent;
			import net.brandonmeyer.containers.SuperPanel;
			import smartx.bam.flex.modules.gis.GisMap;
			import smartx.flex.components.util.MetadataTempletUtil;
			import smartx.flex.components.vo.MetadataTemplet;
			
			protected function testBtn_clickHandler(event:MouseEvent):void{
				var metadataTemplet:MetadataTemplet = MetadataTempletUtil.getInstance().findMetadataTemplet("MT_GIS_APP_test");
				
				if(metadataTemplet == null){
					SmartXMessage.show("无法打开GIS应用！");
					return;
				}
				
				var gisApp:XML = metadataTemplet.contentXML;
				
				var entityModelCode:String = gisApp.@entityModelCode;
				//查找领域实体模型元数据
				var entityModelMt:MetadataTemplet =  MetadataTempletUtil.getInstance()
					.findMetadataTemplet(BAMConst.ENTITY_MODEL_MT_PREFIX.concat(entityModelCode));
				if(entityModelMt == null){
					SmartXMessage.show("无法打开GIS应用！");
					return;
				}
				
				var gisMap:GisMap = new GisMap();
				gisMap.mapUrl = String(entityModelMt.contentXML.geoServer.@url);
				
				var basicLayers:Array = [];
				var entityLayers:Array = [];
				for each(var basicLayer:XML in gisApp.mapInfo.layers.basicLayers.layer){
					if(String(basicLayer.@isDisplay) == "true")
						basicLayers.push(basicLayer);
				}
				for each(var entityLayer:XML in gisApp.mapInfo.layers.entityLayers.layer){
					if(String(entityLayer.@isDisplay) == "true")
						entityLayers.push(entityLayer);
				}
				gisMap.basicLayers = basicLayers;
				gisMap.entityLayers = entityLayers;
				gisMap.centerPoint = String(gisApp.mapInfo.centerPoint);
				gisMap.zoom = String(gisApp.mapInfo.zoom);
				gisMap.srid = String(gisApp.@srid);
				gisMap.contentXml = gisApp;
				
				var superPanel:SuperPanel = new SuperPanel();
				superPanel.allowClose = true;
				superPanel.allowResize = true;
				superPanel.title = "GIS应用";
				superPanel.addEventListener(CloseEvent.CLOSE,function(event:CloseEvent):void{
					PopUpManager.removePopUp(superPanel);
				});
				
				PopUpManager.addPopUp(superPanel,this.root,true);
				
				superPanel.maximize();
				
				superPanel.addEventListener(FlexEvent.CREATION_COMPLETE,function(event:FlexEvent):void{
					superPanel.addChild(gisMap);
				})
				
//				superPanel.callLater(function(gisMap:GisMap):void{
//					superPanel.addChild(gisMap);
//				},[gisMap]);
//				superPanel.addChild(gisMap);
				
				
				
			}
		]]>
	</fx:Script>
	<mx:VBox width="100%" height="100%" paddingBottom="5" paddingLeft="10" paddingRight="5" paddingTop="5" verticalAlign="middle">
		<mx:HBox width="100%" horizontalAlign="left">
			<mx:Label text="Host地址" width="20%" />
			<mx:TextInput id="urlTxt" text="{geoServerXml.@url}" width="50%" change="urlTxt_changeHandler(event)" />
		</mx:HBox>
		<mx:HBox width="100%" horizontalAlign="left">
			<mx:Label text="用户名" width="20%" />
			<mx:TextInput id="userTxt" text="{geoServerXml.@user}" width="50%" change="userTxt_changeHandler(event)" />
		</mx:HBox>
		<mx:HBox width="100%" horizontalAlign="left">
			<mx:Label text="密码" width="20%" />
			<mx:TextInput id="pwdTxt" displayAsPassword="true" text="{geoServerXml.@pwd}" width="50%" change="pwdTxt_changeHandler(event)" />
		</mx:HBox>
		<mx:HBox width="100%" horizontalAlign="left">
			<mx:Label text="WorkSpace" width="20%" />
			<mx:ComboBox id="wsCbo" dataProvider="{wsDp}" change="wsCbo_changeHandler(event)" width="50%" />
			<mx:Button id="queryAllWsBtn" horizontalGap="10" label="初始化" click="queryAllWsBtn_clickHandler(event)" />
		</mx:HBox>
		<mx:HBox width="100%" horizontalAlign="left">
			<mx:Label text="DataStore" width="20%" />
			<mx:ComboBox id="dsCbo" dataProvider="{dsDp}" change="dsCbo_changeHandler(event)" width="50%" />
			<mx:Button id="layoutManagerBtn" horizontalGap="10" label="图层管理" click="layoutManagerBtn_clickHandler(event)" />
			<mx:Button id="testBtn" horizontalGap="10" label="地图测试" click="testBtn_clickHandler(event)" />
		</mx:HBox>
		<mx:HRule width="100%"/>
		<mx:HBox width="100%" horizontalAlign="center">
			<mx:Button label="确认" click="PopUpManager.removePopUp(this)" />
			<mx:Button label="取消" click="PopUpManager.removePopUp(this)" />
		</mx:HBox>
	</mx:VBox>
</mx:TitleWindow>
